***************************************************************************************;
* Macro    : ChkDir                                                                   *;
* Author   : Pieter Kirsten                                                           *;
* Purpose  : Check to see if a directory is created. If not then create the directory.*;
*          : This only works for single level directories                             *;
* Usage    : %chkdir(/data/Personal Folders/Pieter/lev1)                              *;
* Parameters                                                                          *;
* &dir     : specify the directory path to check                                      *;
***************************************************************************************;
* Date      Author            Description                                             *;
* 01JUL2015 Pieter Kirsten    Adapt for general purpose.                              *;
***************************************************************************************;

%macro ChkDir(dir);
   %local rc fileref;
   %let rc = %sysfunc(filename(fileref,&dir));
   %if %sysfunc(fexist(&fileRef))  %then
      %put NOTE: The directory "&dir" exists;
   %else
     %do;
         %sysexec mkdir   "&dir";
         %put NOTE: The directory "&dir" has been created.;
   %end;
   %let rc=%sysfunc(filename(fileref));
%mend;
