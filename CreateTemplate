***************************************************************************************;
* Program:CreateTemplate                                                              *;
* GitHub :https://github.com/p1kos/SASTIP/blob/master/CreateTemplate                  *;
* Purpose:Create a Template table that contains all the variable names that you expect*;
* Usage  :%CreateTemplate(work,myTemplate,%quote(var1,var2,var3,var4,var5)            *;
* &lib   :specify the SAS library to save the template to                             *;
* &ds    :Specify the SAS table name to create                                        *;
* &var   :List of variables to create separated by commax                             *;
***************************************************************************************;
%macro CreateTemplate(lib,ds,var);
   * Get the current quotelenmax option *;
   %let StoreOptionQuoteLenmax=%sysfunc(getoption(QuoteLenMax));
   %put &=StoreOptionQuoteLenmax;

   * Suppress the note in the log about quotes that are longer than 262 bytes long. *;
   options NoQuoteLenMax;
   * Create table with variable names *;
   data &lib..&ds.;
   length NAME $32.;
   numVars=countw("&var.",',');
   do VARNUM = 1 to numVars;
      NAME=scan("&var.",VARNUM,',');
      output;
   end;
   run;

   * Put back the original QuoteLenMax option *;
   options &StoreOptionQuoteLenmax.;
%mend CreateTemplate;
