***************************************************************************************;
* Program:VerifyTableExist                                                            *;
* GitHub :https://github.com/p1kos/SASTIP/blob/master/VerifyTableExist                *;
* Purpose:Verifies the existence of data set and create a macro variable if requested *;
* dsname :Input table name you want to check                                          *;
* macvar :(Optional) Specify a macro variable you want to create                      *;
* Usage  :%IF %VerifyTableExist(sashelp.cars) EQ Y %THEN                              *;
* Usage  :%VerifyTableExist(sashelp.cars,FlagCars);                                   *;
* Credit :Thanks to Adil Vural for the code                                           *;
***************************************************************************************;
%MACRO VerifyTableExist(dsname,macvar);
   %LOCAL value;
   %IF %SYSFUNC(EXIST(&dsname,DATA)) 
   %THEN %LET value=Y;
   %ELSE %LET value=N;

   %IF &macvar NE %THEN %DO;
      %GLOBAL &macvar;
      %LET &macvar=&value;
   %END;
   %ELSE &value.;
%MEND VerifyTableExist;
